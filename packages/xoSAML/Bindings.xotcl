package provide xoSAML::Bindings 0.1

package require XOTcl
package require xoSAML::Objects
package require xoSAML::Http::Client
package require base64

namespace eval ::xoSAML::Bindings {

proc load {binding targetNamespace} {
	namespace eval $targetNamespace {
		set binding [uplevel 1 set binding]
		if { $binding eq "none" } {
			set ns ::xoSAML::Objects
		} else {
			set ns ::xoSAML::Bindings::$binding
		}
		foreach r [list Request LogoutRequest NameIDMappingRequest AuthnRequest AssertionIDRequest ManageNameIDRequest] {
			interp alias {}        $r {} $ns\::Request
			interp alias {} samlp::$r {} $ns\::Request
		}
		foreach r [list ManageNameIDResponse LogoutResponse Response NameIDMappingResponse ArtifactResponse] {
			interp alias {}        $r {} $ns\::Response
			interp alias {} samlp::$r {} $ns\::Response
		}
	}
}
	
namespace eval HTTP::Post {
	::xotcl::Class Request  -superclass ::xoSAML::Objects::Request
	::xotcl::Class Response -superclass ::xoSAML::Objects::Response
	
	Request instproc send {receiver} {
		set client [::xoSAML::Http::Client new]
		$client addQuery SAMLRequest [::base64::encode -wrapchar "" [my export]] 
		$client getUrl $receiver
		return $client
	}
	
	Response instproc receive {sender} {
		set doc [[dom parse $sender] documentElement]
		set input [[$doc find "name" "SAMLResponse"] getAttribute "value"]
		set response [::base64::decode $input]
		my object [::xoSAML::unmarshal $response]
		return [self]
	}
}

}
