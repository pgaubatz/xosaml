package provide xoSAML 0.1

package require XOTcl
package require xoXSD
package require xoSAML::Schema

proc xoSAML {script} {
	namespace eval ::xoXSD::sandbox $script
}

namespace eval ::xoXSD::sandbox addNamespace "::xoSAML" 0

namespace eval ::xoSAML {
	
namespace export *

::xotcl::Class NameIdentifier	-superclass ::xoXSD::CoreObjectWrapper
::xotcl::Class Subject		-superclass ::xoXSD::CoreObjectWrapper
::xotcl::Class Response		-superclass ::xoXSD::CoreObjectWrapper
::xotcl::Class Request		-superclass ::xoXSD::CoreObjectWrapper
::xotcl::Class Status		-superclass ::xoXSD::CoreObjectWrapper
::xotcl::Class Assertion	-superclass ::xoXSD::CoreObjectWrapper
::xotcl::Class Condition	-superclass ::xoXSD::CoreObjectWrapper
::xotcl::Class Request		-superclass ::xoXSD::CoreObjectWrapper
::xotcl::Class Attribute	-superclass ::xoXSD::CoreObjectWrapper

Request instproc init { {type ""} } {
	if { $type eq "" } {
		my type "::xoSAML::samlp::AuthnRequest"
	} else {
		my type $type
	}

	next
	
	#set reasonable defaults...
	my Version "2.0"
	my IssueInstant now
}

Response instproc init args {
	my type "::xoSAML::samlp::Response"
	
	next
	
	#set reasonable defaults
	my Version "2.0"
	my IssueInstant now
}

Status instproc init {code} {	
	my type "::xoSAML::samlp::Status"
	
	next
	
	set prefix "urn:oasis:names:tc:SAML:2.0:status:"
	set codes [list \
		"Success" \
		"Requester" \
		"Responder" \
		"VersionMismatch" \
		"AuthnFailed" \
		"InvalidAttrNameOrValue" \
		"InvalidNameIDPolicy" \
		"NoAuthnContext" \
		"NoAvailableIDP" \
		"NoPassive" \
		"NoSupportedIDP" \
		"PartialLogout" \
		"ProxyCountExceeded" \
		"RequestDenied" \
		"RequestUnsupported" \
		"RequestVersionDeprecated" \
		"RequestVersionTooHigh" \
		"RequestVersionTooLow" \
		"ResourceNotRecognized" \
		"TooManyResponses" \
		"UnknownAttrProfile" \
		"UnknownPrincipal" \
		"UnsupportedBinding" \
	]
	
	set found [lsearch -inline -nocase $codes $code]
	
	if { $found eq "" } {
		puts "ERROR: \"$code\" doesn't seem to be a valid StatusCode."
		return
	}
	
	set code [my createSandboxedObject StatusCode]
	$code Value "$prefix$found"
	my StatusCode $code
}

Assertion instproc init args {
	my type "::xoSAML::saml::Assertion"
	
	next
	
	#set reasonable defaults
	my Version "2.0"
	my IssueInstant now
}

Subject instproc init args {
	my type "::xoSAML::saml::Subject"
	
	next
}

Attribute instproc init {{attrName ""} {attrValue ""}} {
	my type "::xoSAML::saml::Attribute"
	
	next
	
	my NameFormat "urn:oasis:names:tc:SAML:2.0:attrname-format:basic"
	if { $attrName ne "" } {
		my Name $attrName
	}
	if { $attrValue ne "" } {
		set val [my createSandboxedObject saml:AttributeValue $attrValue]
		my AttributeValue $val 
	}
}

}
