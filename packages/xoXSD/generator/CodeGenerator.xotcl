package provide xoXSD::CodeGenerator 0.1

package require tdom
package require XOTcl

namespace import -force ::xotcl::*

namespace eval ::xoXSD {

::xotcl::Class CodeGenerator -slots {
	::xotcl::Attribute objects
	::xotcl::Attribute nsmap -default ""
}

CodeGenerator instproc parse {data {nsmap ""}} {
	if { $data == "" } return
	
	my objects [list]
	if { $nsmap != "" } {
		my nsmap $nsmap
	}
	
	set doc [dom parse $data] 
	set root [$doc documentElement]
	
	my handleNode $root
	
	set o ""
	foreach object [my objects] {
		foreach line $object {
			append o "$line\n"
		}
		append o "\n"
	}
	return $o
}

CodeGenerator instproc getPrefix {node} {
	set name [$node nodeName]
	set pos [string last ":" $name]
	if { $pos != -1 } { 
		return [string range $name 0 [expr {$pos - 0}]]
	}
	
	if { [my nsmap] == "" } {
		return ""
	}
	set nsuri [$node namespaceURI]
	foreach ns [my nsmap] {
		if { [$node namespaceURI] == [lindex $ns 1] } {
			return "[lindex $ns 0]\:"
		}
	}
	return ""
}
	
CodeGenerator instproc getName {node} {
	set name [my stripPrefix [$node nodeName]]
	set prefix [my getPrefix $node]
	if { $prefix == "" } {
		puts "#WARNING: Could not determine a XML namespace prefix for \"$name\" (namespace: [$node namespaceURI])"
	}
	return "$prefix$name"
}
	
CodeGenerator instproc stripPrefix {name} {
	set pos [string first ":" $name]
	if { $pos == -1 } { return $name }
	return [string range $name [expr {$pos + 1}] end]
}

CodeGenerator instproc stripInitString {name} {
	set pos [string first "\"" $name]
	if { $pos == -1 } { return $name }
	return [string range $name 0 [expr {$pos - 2}]]
}

CodeGenerator instproc handleNode {node} {
	set nodeName [$node nodeName]
	if { $nodeName == "#comment" || $nodeName == "#cdata" } {
		return
	}
	
	set name [my getName $node]
	set objname [Object autoname obj]
	set obj [list]
	
	set init "$name $objname"
	foreach child [$node childNodes] {
		if { [$child nodeName] == "#text" } {
			set text [string trim [$child nodeValue]]
			set text [string map {"\n" "\\n" "\r" "\\r"} $text]
			append init " \"$text\""
			$child delete
			break
		}
	}
	
	lappend obj $init
	
	if { [$node hasAttribute "xmlns"] } {
		$node removeAttribute "xmlns"
	}
	foreach attribute [$node attributes "xmlns:*"] {
		$node removeAttribute "xmlns:[lindex $attribute 0]"
	}
	
	foreach attribute [$node attributes] {
		if { [lindex $attribute 1] != "" } continue
		lappend obj "$objname $attribute \"[$node getAttribute [lindex $attribute 0]]\""
	}
	
	foreach child [$node childNodes] {
		set childobj [my stripPrefix [my stripInitString [lindex [my handleNode $child] 0]]]
		lappend obj "$objname $childobj"
	}
	
	my lappend objects $obj
	return $obj
}
	
}
