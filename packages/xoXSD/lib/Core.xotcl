package provide xoXSD::Core 0.1

package require tdom
package require XOTcl
package require xoXSD::DataTypes

namespace eval ::xoXSD::Core {
	
##############################################
#               * Definition *               #
##############################################	
	
::xotcl::Class Generic		-superclass ::xotcl::Class -slots {
	::xotcl::Attribute rootClass	
	::xotcl::Attribute elements
	::xotcl::Attribute attributes
	::xotcl::Attribute childslots
	::xotcl::Attribute children
}
	
::xotcl::Class GenericChild	-superclass ::xoXSD::Core::Generic

::xotcl::Class Element		-superclass ::xoXSD::Core::Generic 

::xotcl::Class ComplexType	-superclass ::xoXSD::Core::Generic

::xotcl::Class Sequence		-superclass ::xoXSD::Core::GenericChild -slots {
	::xotcl::Attribute sequence -default false
}

::xotcl::Class Choice		-superclass ::xoXSD::Core::GenericChild -slots {
	::xotcl::Attribute slotfree 	 -default true
	::xotcl::Attribute choiceparents -default ""
}

namespace export *
	
##############################################
#              * Constructors *              #
##############################################

Generic instproc init { {rootClass ""} } {
	if { $rootClass == "" } {
		my rootClass [my class]
	} else {
		my rootClass $rootClass
	}
	
	set heritage [list]
	foreach c [[my class] info heritage] {
		if { [namespace qualifiers $c] == "::xoXSD::Core" } break
		lappend heritage $c
	}

	my children [list]
	my childslots [list]
	my elements [list]
	my attributes [list]
	
	# create new instances of all classchildren
	foreach c [concat [my class] [lreverse $heritage]] {
		foreach	child [$c info classchildren] {
			set ch [eval $child new -childof [self] [my rootClass]]
			my [namespace tail [$ch class]] $ch
			my lappend children $ch
		}
	}
	
	foreach slot [my getLocalSlots] {
		set slotclass [namespace tail [$slot class]]
		
		if { $slotclass == "XMLAttribute" } {
			my addAttribute $slot
		} elseif { $slotclass == "XMLElement" } {
			my addElement $slot
		} elseif { $slotclass == "XMLChild" } {
			my lappend childslots $slot
		}
	}	
}

Sequence instproc init args {
	# Generic->init
	next 
	
	set slots [list]
	foreach slot [my getLocalSlots] {
		if { [namespace tail [$slot class]] == "XMLAttribute" } {
			continue
		}
		lappend slots [list $slot [$slot sequence]]
	}
	
	my sequence [list]
	foreach slot [lsort -integer -index 1 $slots] {
		set slot [lindex $slot 0]
		set slotname [namespace tail $slot]
		set slotclass [namespace tail [$slot class]]
		
		if { $slotclass == "XMLChild" } {
			foreach child [my children] {
				set childclass [namespace tail [$child class]]
				if { $childclass == $slotname } {
					my lappend sequence $child	
				}
			}
		} else {
			my lappend sequence $slot
		}
	}
	
	# add the missing childclasses (with no sequence)
	foreach child [my children] {
		if { [lsearch [my sequence] $child] == -1 } {
			my lappend sequence $child
		}
	}
}

Choice instproc init args {
	# Generic->init
	next 	
	
	# get "choice-parents"...
	set parent [my info parent]
	while { [Object isobject $parent] } {
		set cls [namespace tail [$parent class]]
		if { [string match "__choice*" $cls] } {
			my lappend choiceparents $parent	
		}
		set parent [$parent info parent]
	}
}


##############################################
# * Various "publicly" available functions * #
##############################################

Generic instproc check {} {	
	foreach slot [my attributes] {
		if { [$slot use] == "required" && ![my slotFilled $slot] } {
			my error "needs object: [namespace tail $slot] (type: [$slot type])"
			return false
		}
	}
	foreach slot [my elements] {
		if { ![my checkMinOccurs $slot] } { 
			return false 
		}
	}
	foreach child [my getAllChildren] {
		if { ![$child check] } {
			return false
		}
	}
	
	return true
}
	
Choice instproc check {} {
	if { [my slotfree] } { 
		return true 
	}
	
	set choice ""
	foreach slot [my elements] {
		if { [my slotFilled $slot] } {
			set choice $slot
			break
		}
	}	
	if { $choice == "" } { 
		return true 
	}
	if { ![my checkMinOccurs $choice] } {
		return false
	}
	foreach slot [my getSlot $choice] {
		if { ![$slot check] } {
			return false
		}
	}
	return true
}

Generic instproc export { {addXmlNamespaces true} {name ""} } {
	if { ![my check] } {
		my error "is not valid."
		return ""
	}
	
	# create the document:
	set doc [dom createDocument [my getXmlElementExportName]]
	set root [$doc documentElement]
	
	# add the xmlns-attributes:
	if { $addXmlNamespaces } {
		set prefix [my getXmlPrefix]	
		$root setAttribute "xmlns:$prefix" [my getXmlNamespace]
		my setXmlnsAttributes $root	
	}
	
	# add the attributes:
	my exportAttributes $doc $root
	
	# add the containing sub-elements:
	my exportElements $doc $root	

	return [$root asXML]
}

Generic instproc print {} {
	puts "Exporting [self] ([my class]) ..."
	puts [my export]
}

Generic instproc printContent {} {
	set content [my getContent]
	if { $content == false } {
		my error "doesn't have any textual content!"
		return
	}
	puts "The textual content of [self] ([my class]) is:\n$content"
}

Generic instproc getSlot {slot} {
	set s [my doGetSlot $slot]
	if { $s == false } {
		my error "there is no such Element/Attribute to be get: $slot"	
		return false
	} 
	return $s
}

Generic instproc getSlotClass {slot {withChildSlots false}} {
	set slotclass [my doGetSlotClass $slot $withChildSlots]
	if { $slot == false } {
		my error "there is no such slot: $slot"	
		return false
	} 
	return $slotclass
}

Generic instproc getContent {} {
	set content [my getSlot __text]
	if { $content == false } {
		return ""
	}
	return [$content getContent]
}

Generic instproc setSlot {slot value} {
	if { [my doSetSlot $slot $value] == true } {
		return true
	} else {
		my error "there is no such Element/Attribute to be set: $slot"	
		return false
	}
}

Generic instproc initSlot {slot args} {	
	set slotclass [my getSlotClass $slot]
	if { $slotclass == false } {
		my error "there is no such slot: $slot"
		return false
	}
	set slottype [$slotclass type]
	if { [namespace qualifiers $slottype] != "::xoXSD::DataTypes" } {
		my error "\"$slot\" is no basic data type."
		return false
	}
	set content [$slottype new $args]
	if { [$content valid] } {
		my setSlot $slot $content
	} else {
		my error "the content of \"$slot\" is not valid."
		return false
	}
}
	
Generic instproc printSlots {} {
	# first get a list of all elements and attributes:
	set elements   [my elements]
	set attributes [my attributes]
	foreach child [my getAllChildren] {
		set elements   [concat $elements   [$child elements  ]]
		set attributes [concat $attributes [$child attributes]]
	}
	
	set classname [namespace tail [my class]]
	set headers [list "Name" "Type" "Additional Information"]
	
	if { [llength $elements] > 0 } {
		set content [list]
		foreach e $elements {
			lappend content [list [$e getName $e] [$e getType] [$e getInfo]]
		}
		puts [my makeTable "Elements of \"$classname\"" $headers $content]
	}
	if { [llength $attributes] > 0 } {
		set content [list]
		foreach a $attributes {
			lappend content [list [$a getName $a] [$a getType] [$a getInfo]]
		}
		puts [my makeTable "Attributes of \"$classname\"" $headers $content]
	}
}
	
Generic instproc addAny {slot} {
	if { [$slot procsearch getObject] != "" } {
		set slot [$slot getObject]
	}
	set slotname [namespace tail [$slot class]]
	
	# first check if the element actually has an <any> element:
	set any [my getSlotClass __any]
	if { $any == false } {
		my error "there is no <any> element to be set."
		return false
	}
	
	# search for the class/instance that has the __any slot defined:
	set cls [[$any info parent] info parent]
	set inst ""
	foreach inst [concat [self] [my getAllChildren]] {
		set c [$inst class]
		if { $c == $cls || [lsearch [$c info heritage] $cls] != -1 } { 
			break
		}
	}
	if { $inst == "" } { error "should not reach here. didn't find inst!" }
	
	# check if a slot named $slotname already exists:
	if { [$inst slotExists $slotname] } {
		my error "there's is already a slot named \"$slotname\"."
		return false
	}
	
	# check for maxOccurs restrictions:
	set anyslot "$cls\::slot\::__any"
	set anyCounter [$anyslot anyCounter]
	set maxOccurs [$anyslot maxOccurs]
	if { $maxOccurs != "unbounded" } {
		if { $anyCounter >= $maxOccurs } {
			my error "cannot be set because of a maxOccurs restriction."
			return false
		}
	}
	
	# check for namespace restrictions:
	set namespaces [split [$anyslot namespace] " "]
	if { ![my checkAnyNamespaceRestrictions $namespaces $inst $slot] } {
		my error "cannot be set because of namespace restrictions."
		return false
	}
	
	# dynamically create and add a new slot to the class
	$cls slots "::xoXSD::Slots::XMLElement $slotname -type [$slot class]"
	$inst addElement "$cls\::slot\::$slotname"
	
	# finally set the newly created slot
	my setSlot $slotname $slot
	
	# update the anyCounter if necessary
	if { $maxOccurs != "unbounded" } {
		$anyslot anyCounter [incr anyCounter]
	}
}
	
Generic instproc addAnyAttribute {slotname type value} {
	if { [Object isobject $value] } {
		if { [$value procsearch getObject] != "" } {
			set value [$value getObject]
		}
	}
	
	# first check if the element actually has an <any> element:
	set anyAttribute [my getSlotClass __anyAttribute]
	if { $anyAttribute == false } {
		my error "there is no <anyAttribute> attribute to be set."
		return false
	}
	
	# search for the class/instance that has the __anyAttribute slot defined:
	set cls [[$anyAttribute info parent] info parent]
	set inst ""
	foreach inst [concat [self] [my getAllChildren]] {
		set c [$inst class]
		if { $c == $cls || [lsearch [$c info heritage] $cls] != -1 } { 
			break
		}
	}
	if { $inst == "" } { error "should not reach here. didn't find inst!" }
	
	# check for namespace restrictions:
	set anyslot "$cls\::slot\::__anyAttribute"
	set namespaces [split [$anyslot namespace] " "]
	set content [$type new $value]
	if { ![my checkAnyNamespaceRestrictions $namespaces $inst $content] } {
		my error "cannot be set because of namespace restrictions."
		return false
	}
	
	# dynamically create and add a new slot to the class
	$cls slots "::xoXSD::Slots::XMLAttribute $slotname -type $type"
	$inst addAttribute "$cls\::slot\::$slotname"
	
	# finally set the newly created slot
	my setSlot $slotname $content
}

##############################################
#         * Various helper functions *       #
# Note: you shouldn't call them directly.    #
##############################################

Generic instproc exportAttributes {doc root} {
	# iterate over all attributes
	foreach attribute [my attributes] {
		my exportAttribute $doc $root $attribute
	}
	
	# do the same with all children
	foreach child [my children] {
		$child exportAttributes $doc $root	
	}
}

Generic instproc exportAttribute {doc root attribute} {
	if { ![my slotFilled $attribute] } return
	
	set content [[my [namespace tail $attribute]] getContent]
	if { [$attribute text] } {
		$root appendChild [$doc createTextNode $content]
	} else {	
		$root setAttribute [namespace tail $attribute] $content
	}
}

Generic instproc exportElements {doc root} {
	# iterate over all elements
	foreach element [my elements] {
		my exportElement $doc $root $element
	}
	
	# do the same with all children
	foreach child [my children] {
		$child exportElements $doc $root	
	}
}

Sequence instproc exportElements {doc root} {
	foreach obj [my sequence] {
		if { [string match "*::slot::*" $obj ] } {
			my exportElement $doc $root $obj
		} else {
			$obj exportElements $doc $root	
		}
	}
}

Generic instproc exportElement {doc root element} {
	if { ![my slotFilled $element] } return
	set name [my getXmlElementExportName $element] 
	foreach element [my [namespace tail $element]] {
		$root appendXML [$element export false $name]
	}
}

Generic instproc setXmlnsAttributes {root} {
	foreach element [my elements] {
		set element [namespace tail $element]
		if { ![my slotFilled $element] } continue
		
		set element [lindex [my $element] 0]
		set ns [$element getXmlNamespace]
		set prefix [$element getXmlPrefix]
		
		$root setAttribute "xmlns:$prefix" $ns
	}	
	foreach child [my children] {
		$child setXmlnsAttributes $root
	}
}
	
Generic instproc getXmlElementExportName { {element ""} } {
	if { $element == "" } { 
		set element [my class] 
	}
	set name [namespace tail $element]
	set prefix [my getXmlPrefix]
	if { $prefix != "" } {
		set name "$prefix:$name"
	}	
	return $name
}

Generic instproc getRootNamespaceVariable {varname} {
	set qualifiers [namespace qualifiers [my rootClass]]
	if { [namespace eval $qualifiers info exists $varname] } {
		namespace upvar $qualifiers $varname var
		return $var
	}
	return ""
}

Generic instproc getXmlNamespace {} {
	return [my getRootNamespaceVariable xmlNamespace]
}

Generic instproc getXmlPrefix {} {
	return [my getRootNamespaceVariable xmlPrefix]
}

Generic instproc getSlots {{withChildSlots false}} {
	if { $withChildSlots } {
		return [concat [my elements] [my attributes] [my childslots]]
	} 
	return [concat [my elements] [my attributes]]
}

Generic instproc getLocalSlots {} {
	# now create a list of all slots
	set slots [[my class] info slots]
	
	# remember slotnames
	foreach slot $slots {set slotname([namespace tail $slot]) 1}
	
	# iterate over class structure
	set heritage [[my class] info heritage]
	foreach c [lsearch -inline -all -not $heritage "::xoXSD::Core::*"] {
		foreach slot [$c info slots] {
			set key slotname([namespace tail $slot])
			
			# don't add slots which are already defined in 
			# more specialized classes
			if {[info exists $key]} continue
			set $key 1
			lappend slots $slot
		}
	}
	
	return $slots
}

Generic instproc getSlotType {slot} {
	# returns either XMLAttribute, XMLElement, XMLChild or false
	set slotclass [my getSlotClass $slot]
	if { $slotclass == false } { return false }
	return [namespace tail [$slotclass class]]
}

Generic instproc doGetSlot {slot} {
	set slot [namespace tail $slot]
	
	if { ![my slotExists $slot] || ![my exists [namespace tail $slot]] } {
		foreach child [my children] {
			set ret [$child doGetSlot $slot]
			if { $ret != false } { return $ret }
		}
		# there is no such slot:
		return false
		
	}

	set parent [my info parent]
	if { [Object isobject $parent] } {
		set classname [namespace tail [my class]]
		set c [$parent getSlotClass $classname true]
	
		if { [$c multivalued] } {
			set slots [list]
			foreach child [$parent $classname] {
				if { [$child exists $slot] } {
					lappend slots [$child $slot]
				}
			}
			if { [llength $slots] == 0 } {
				return false
			}
			return $slots
		}
	}
	
	return [my $slot]
}

Generic instproc doSetSlot {slot value} {
	set slot [namespace tail $slot]
	if { ![my slotExists $slot] } {
		if { [my children] == "" } { return -1 }
		foreach child [my children] {
			set result [$child doSetSlot $slot $value]
			if { $result != -1 } { return $result } 		
		}
		return -1
	} 
	
	set c [my getSlotClass $slot]
	
	#Check if the given value-object matches the required type
	if { [$c type] != [$value class] } {
		my error "\"$slot\" has to be of type [$c type]"
		return false
	}
	
	#Check for a simpleType's enumeration restriction
	set enum [list]
	if { [$c exists enumvalues] } {
		set enum [$c enumvalues]
	}
	if { [llength $enum] > 0 } {
		if { [lsearch -exact $enum [$value getContent]] == -1 } {
			my error "\"$slot\" is not correctly formatted."
			return false
		}
	}
	
	#finally set slot
	if { [$c multivalued] && [my slotFilled $slot] } {
		set occurs [llength [my $slot]]
		set maxOccurs [$c maxOccurs]
		if { $maxOccurs == "unbounded" || $occurs < $maxOccurs } {
			my $slot add $value
		} else {
			my error "\"$slot\" cannot be set because of a maxOccurs restriction."
			return false
		}
	} else {
		my $slot $value
	}
	return true
	
}

Choice instproc doSetSlot {slot value} {
	if { [my slotfree] } {
		if { [next] == true } {
			my slotfree false
			foreach parent [my choiceparents] {
				$parent slotfree false	
			}
			return true
		} else {
			return -1
		}
	} elseif { ![my slotfree] && [my slotExists $slot] } {
		set ret [my tryCreateChild $slot $value]
		if { $ret != -1 } {
			return $ret
		}
			
		if { [my children] == "" } { return -1 }
		foreach child [my children] {
			set result [$child doSetSlot $slot $value]
			if { $result != -1 } { return $result }
		}
		error "Shouldn't reach here!"
	} else {
		my error "is already set by another Element/Attribute."
		return false
	}
}
	
Sequence instproc doSetSlot {slot value} {
	set ret [next]
	if { $ret != -1 } {
		return $ret
	}
	return [my tryCreateChild $slot $value]
}	
	
GenericChild instproc tryCreateChild {slot value} {
	set name [namespace tail [my class]]
	set parent [my info parent]
	set c [$parent getSlotClass $name true]
	
	if { ![$c multivalued] } {
		return -1
	} 
	
	set occurs [llength [$parent $name]]
	set maxOccurs [$c maxOccurs]
	if { $maxOccurs == "unbounded" || $occurs < $maxOccurs } {
		set ch [eval [my class] new -childof $parent [my rootClass]]
		
		$parent $name add $ch
		$parent addChild $ch
		
		return [$ch doSetSlot $slot $value]
	} 

	return -1
}

Generic instproc doGetSlotClass {slot {withChildSlots false}} {
	set slot [namespace tail $slot]
	if { ![my slotExists $slot $withChildSlots] } {
		foreach child [my children] {
			set result [$child doGetSlotClass $slot]
			if { $result != false } { return $result }
		}
		return false
		
	} else {
		return [lsearch -inline -glob [my getSlots $withChildSlots] "*::$slot"]
	}
}

Generic instproc error {msg} {
	puts "XML-Error: [self] (type: [my class]) $msg"
}

Generic instproc slotExists {slot {withChildSlots false}} {
	set existance [lsearch [my getSlots $withChildSlots] "*::slot::[namespace tail $slot]"]
	return [expr { $existance != -1 } ]
}

Generic instproc slotFilled {slot} {
	return [my exists [namespace tail $slot]]
}

Generic instproc getAllChildren {} {
	set children [my children]
	foreach child [my children] {
		set children [concat $children [$child getAllChildren]]
	}
	return $children
}
	
Generic instproc addElement {element} {
	my lappend elements $element
}
		
Sequence instproc addElement {element} {
	next
	my lappend sequence $element
}	
	
Generic instproc addAttribute {attribute} {
	my lappend attributes $attribute
}

Generic instproc addChild {child} {
	my lappend children $child
}

Sequence instproc addChild {child} {
	next
	my lappend sequence $child
}
	
Generic instproc checkAnyNamespaceRestrictions {namespaces inst value} {	
	if { [lsearch $namespaces "##any"] != -1 } {
		return true
		
	} elseif { [lsearch $namespaces "##other"] != -1 } {
		if { [$value getXmlNamespace] != [$inst getXmlNamespace] } {
			return true
		} 
		
	} elseif { [lsearch $namespaces "##targetNamespace"] != -1 } {
		if { [$value getXmlNamespace] == [$inst getXmlNamespace] } {
			return true
		} 
		
	} elseif { [lsearch $namespaces "##local"] != -1 } {
		if { [$value getXmlNamespace] == "" } {
			return true
		} 
		
	} else {
		foreach ns $namespaces {
			if { $ns == [$value getXmlNamespace] } {
				return true
			}
		}
	}
	
	return false
}

Generic instproc checkMinOccurs {slot} {
	set name [namespace tail $slot]
	set validity true
	if { [$slot minOccurs] > 0 } {
		if { ![my slotFilled $slot] } {
			set validity false
			my error "needs object: $name (type: [$slot type])"
			
		} elseif { [llength [my $name]] < [$slot minOccurs] } {
			set validity false
			my error "needs [$slot minOccurs] instances of $name (type: [$slot type])"
		}	
	}	
	return $validity
}

Generic instproc makeTable {tableheader headers content} {
	set width [list]
	foreach header $headers {
		lappend width [string length $header]
	}
	foreach line $content {
		for {set i 0} {$i < [llength $line]} {incr i} {
			set colMax 0
			foreach colLine [lindex $line $i] {
				set colMax [expr max($colMax, [string length $colLine])]	
			}
			lset width $i [expr max($colMax, [lindex $width $i])]
		}
	}
	
	set sep "+"
	set lw 0
	foreach w $width {
		incr lw $w
		append sep "-[string repeat - $w]-+"
	}
	set o "+[string repeat - [expr $lw + 3 * [llength $headers] - 1]]+\n"
	append o "| [format "%-*s | " [expr $lw + 2 * [llength $headers]] $tableheader]\n"
	
	append o "$sep\n[my makeTableRow $width $headers]$sep\n"
	foreach line $content {
		set colLinesMax 1
		foreach colLines $line {
			set colLinesMax [expr max($colLinesMax, [llength $colLines])]
		}
		set content [list]
		for {set i 0} {$i < $colLinesMax} {incr i} {
			set content [list]
			for {set j 0} {$j < [llength $line]} {incr j} {
				lappend content [lindex [lindex $line $j] $i]
			}
			append o [my makeTableRow $width $content]
		}
		append o "$sep\n"
	}
	return $o
}

Generic instproc makeTableRow {width content} {
	set line "| "
	for {set i 0} {$i < [llength $content]} {incr i} {
		append line [format "%-*s | " [lindex $width $i] [lindex $content $i]]
	}
	return "$line\n"
}

Generic instproc getObject {} {
	return [self]
}

Generic instproc unknown {cmd args} {
	set args [join $args]
	
	#read slot
	if { $args == "" } {
		return [my getSlot $cmd]	
	}
	
	#set slot...
	if { [my getSlotType $cmd] == "XMLAttribute" } {
		return [my initSlot $cmd $args]	
	} else {
		return [my setSlot $cmd [$args getObject]]
	}
}

}

